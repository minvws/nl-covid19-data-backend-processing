-- Copyright (c) 2020 De Staat der Nederlanden, Ministerie van   Volksgezondheid, Welzijn en Sport.
-- Licensed under the EUROPEAN UNION PUBLIC LICENCE v. 1.2 - see https://github.com/minvws/nl-contact-tracing-app-coordinationfor more information.
/****** Object:  View [VWSDEST].[V_DIFFERENCE_REPRODUCTION_INDEX_LAST_KNOWN_AVERAGE__REPRODUCTION_INDEX_AVG]    Script Date: 12-11-2020 14:45:59 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE OR ALTER VIEW [VWSDEST].[V_DIFFERENCE_REPRODUCTION_INDEX_LAST_KNOWN_AVERAGE__REPRODUCTION_INDEX_AVG] AS
--select distinct date_last_inserted based on days, not time
WITH BASE_CTE AS (
SELECT
	DISTINCT CONVERT(date, DATE_LAST_INSERTED) AS [Date]
	FROM [VWSDEST].[REPRODUCTION_NUMBER]
),

--rank those dates from most to least recent
SECOND_CTE AS(
SELECT
	[Date],
	RANK() OVER (ORDER BY [DATE] DESC) AS [RANK]
	FROM BASE_CTE
),

--select the previous date_last_inserted
THIRD_CTE AS (
SELECT 
	[DATE]
	FROM SECOND_CTE
	WHERE [RANK] = 2
),

--select the most recent date_last_inserted from the previous date, including the time
FOURTH_CTE AS (
SELECT MAX(DATE_LAST_INSERTED) AS [PREVIOUS_DATE_LAST_INSERTED]
	FROM [VWSDEST].[REPRODUCTION_NUMBER]
	WHERE CONVERT(date, DATE_LAST_INSERTED) = (SELECT [DATE] FROM THIRD_CTE)
),


--select the most recent date_of_report within the previous date_last_inserted
FIFTH_CTE AS (
SELECT MAX(DATE_OF_REPORT) [PREVIOUS_DATE_OF_REPORT]
	FROM [VWSDEST].[REPRODUCTION_NUMBER] 
	WHERE DATE_LAST_INSERTED = (SELECT [PREVIOUS_DATE_LAST_INSERTED] FROM FOURTH_CTE)
		AND [REPRODUCTION_INDEX_AVG] IS NOT NULL
)


SELECT T1.[DATE_OF_REPORT]
	  ,T1.[DATE_OF_REPORT_UNIX] AS [NEW_DATE_UNIX]
	  ,T2.[DATE_OF_REPORT] AS [PREVIOUS_DATE_OF_REPORT]
	  ,T2.[DATE_OF_REPORT_UNIX] AS [OLD_DATE_UNIX]
      ,CASE WHEN T1.[REPRODUCTION_INDEX_AVG] IS NULL THEN 0 ELSE T1.[REPRODUCTION_INDEX_AVG] END AS [NEW_VALUE]
	  ,CASE WHEN T2.[REPRODUCTION_INDEX_AVG] IS NULL THEN 0 ELSE T2.[REPRODUCTION_INDEX_AVG] END AS [OLD_VALUE]
	  ,CASE WHEN (T1.[REPRODUCTION_INDEX_AVG] - T2.[REPRODUCTION_INDEX_AVG]) IS NULL THEN 0 ELSE (T1.[REPRODUCTION_INDEX_AVG] - T2.[REPRODUCTION_INDEX_AVG]) END AS [DIFFERENCE]
      ,T1.[DATE_LAST_INSERTED]
	  ,T2.[DATE_LAST_INSERTED] AS [PREVIOUS_DATE_LAST_INSERTED]
  FROM [VWSDEST].[REPRODUCTION_NUMBER] T1
  LEFT JOIN (SELECT DATE_OF_REPORT, DATE_OF_REPORT_UNIX, DATE_LAST_INSERTED, [REPRODUCTION_INDEX_AVG]
				FROM [VWSDEST].[REPRODUCTION_NUMBER] 
				WHERE DATE_LAST_INSERTED = (SELECT MAX(DATE_LAST_INSERTED)
											FROM [VWSDEST].[REPRODUCTION_NUMBER])
					AND DATE_OF_REPORT = (SELECT [PREVIOUS_DATE_OF_REPORT] FROM FIFTH_CTE)) T2
	ON 1 =1 
  WHERE T1.DATE_LAST_INSERTED = (SELECT MAX(DATE_LAST_INSERTED)
                            FROM [VWSDEST].[REPRODUCTION_NUMBER])
	AND T1.DATE_OF_REPORT = (SELECT MAX(DATE_OF_REPORT)
                            FROM [VWSDEST].[REPRODUCTION_NUMBER] WHERE [REPRODUCTION_INDEX_AVG] IS NOT NULL
							AND DATE_LAST_INSERTED = (SELECT MAX(DATE_LAST_INSERTED) FROM [VWSDEST].[REPRODUCTION_NUMBER]))
GO
